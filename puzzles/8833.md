// -*- mode: poly-markdown -*-

# Solving the 8 8 3 3 puzzle

The puzzle is as follows: using the numbers 8, 8, 3, and 3, each of which must
be used exactly once, and combining these with the arithmetic operators, produce
24.

```racket
#lang racket
```

My plan is to generate all possible expressions involving those numbers and the
four binary operators (+, -, *, /) and find one whose value is 24. It's not a
very good plan, because it might run out of memory. 

First step is to define "expressions". New product types in Racket are
constructed using structs. The keyword `#:transparent` means that when
the struct is printed, the printer also prints its elements.

```racket
;; An Expression is either
;; - a BinOp,
;; - a number, or
;; - #f

;; A BinOp is a pair of Expressions
(struct BinOp (left-expr right-expr) #:transparent)

(struct Plus BinOp () #:transparent)
(struct Minus BinOp () #:transparent)
(struct Times BinOp () #:transparent)
(struct Div BinOp () #:transparent)
```

The value `#f` (the Boolean value "false") is Racket idiom for `Nothing`. It's
there to represent the result of division by zero so we don't throw a runtime
error. However, we do have to lift all the standard arithmetic operations to
work with `#f`:

```racket
(define (make-safe-op op)
  (λ (x y)
    (if (not (and x y))     ; all values are true except #f
        #f
        (op x y))))

(define safe-+ (make-safe-op +))
(define safe-- (make-safe-op -))
(define safe-* (make-safe-op *))
(define safe-/ (make-safe-op /))
```

To evaluate an Expression, evaluate the subexpressions and combine, noting that
the value of `#f` is `#f`, the value of a number is that number, and the value
of a `Plus`, `Minus`, `Times`, or `Div` is the appropriate (safe) operation:

```racket
;; The value of an Expression
(define (eval-expr expr)
  (match expr
    [#f #f]
    [(? number? N) N]
    [(Plus  left-expr right-expr) (safe-+ (eval-expr left-expr) (eval-expr right-expr))]
    [(Minus left-expr right-expr) (safe-- (eval-expr left-expr) (eval-expr right-expr))]
    [(Times left-expr right-expr) (safe-* (eval-expr left-expr) (eval-expr right-expr))]
    [(Div   left-expr right-expr)
     (let ([right-eval (eval-expr right-expr)])
       (if (eq? right-eval 0)
           #f
           (safe-/ (eval-expr left-expr) right-eval)))]))
```

So, for example
```racket
> (eval-expr (Times (Plus 1 2) 4))
12
```

Okay, that was the easy bit! The plan is to define `expresify-given-numbers`
which, given a list of allowed numbers, produces a list of all valid arithmetic
expressions formed from those numbers. For example,
```racket
> (expressify-given-numbers '(1 2))
(list (Plus 1 2) (Minus 1 2) (Times 1 2) (Div 1 2) (Plus 2 1) (Minus 2 1) (Times 2 1) (Div 2 1))
```
To do so, we first make a list of all permutations of the allowed numbers;
then split each permutation into two at every possible break point. That gives a
list of all possible pairs of sets of numbers. 
```racket
(define (binary-partitions ls)
  (append-map binary-splits (permutations ls)))

(define (binary-splits ls)
  (map (λ (n)
         (call-with-values (λ () (split-at ls n)) cons))
       (range 1 (length ls))))
```

For example,
```racket
> (binary-partitions '(1 2 3))
'(((1) 2 3) ((1 2) 3) ((2) 1 3) ((2 1) 3) ((1) 3 2) ((1 3) 2) ((3) 1 2) ((3 1) 2) ((2) 3 1) ((2 3) 1) ((3) 2 1) ((3 2) 1))
```

Next make all possible combinations of all possible expressions starting with
the numbers in the two parts of the split. To make all possibe combinations of
two expressions:
```racket
;; Given a list of two Expressions, return all binary operations on those expressions
;; binary-ops : (listof Expr?) -> [Listof BinOp?]
(define (binary-ops es)
  (let ([e₁ (car es)]
        [e₂ (cadr es)])
    (list
     (Plus e₁ e₂)
     (Minus e₁ e₂)
     (Times e₁ e₂)
     (Div e₁ e₂))))
```
For example,
```racket
> (binary-ops (list 10 20))
(list (Plus 10 20) (Minus 10 20) (Times 10 20) (Div 10 20))
```
It looks odd to take a list rather than two arguments, but it makes the
following simpler.

```racket
;; Given a list of numbers, return all Expressions involving the numbers in that list exactly once
;; expressify-given-numbers : [listof number?] -> [listof Expression?]
(define (expressify-given-numbers ls)
  (if (eq? (length ls) 1)
      ls
      (append-map binary-ops-from-pair (binary-partitions ls))))

;; Given a pair of lists of numbers, return all binary operations on pairs of expressions from the
;; expansions.
(define (binary-ops-from-pair pr)
  (let ([left-exprs  (expressify-given-numbers (car pr))]
        [right-exprs (expressify-given-numbers (cdr pr))])
    (append-map binary-ops (cartesian-product left-exprs right-exprs))))
```

To solve the puzzle, use `expressify-given-numbers` to make a list of all
expressions, then filter out the ones for which `eval-expr` returns 24. Here's a
non-spoiler version: make 3 from 1, 2 and 3:

```racket
> (filter (λ (e) (equal? (eval-expr e) 3)) (expressify-given-numbers '(1 2 3)))
(list
 (Times (Minus 2 1) 3)
 (Times (Minus 2 1) 3)
 (Times 3 (Minus 2 1))
 (Div 3 (Minus 2 1))
 (Times 3 (Minus 2 1))
 (Div 3 (Minus 2 1)))
```
(Each answer appears twice, so I have done something unecessary and I don't know
what.)









